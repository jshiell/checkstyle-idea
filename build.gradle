apply plugin: 'java'

sourceCompatibility = 1.8
version = '4.13.3'

repositories {
    mavenCentral()
}

configurations {
    ideaSdkLibs
}

dependencies {
    ideaSdkLibs fileTree(dir: "$ideaSdk", include: '*.jar')

    compile(group: 'com.puppycrawl.tools', name: 'checkstyle', version: '6.4.1') {
        exclude group: 'commons-logging', module: 'commons-logging'
    }
    compile group: 'commons-io', name: 'commons-io', version: '2.4'

    testCompile group: 'junit', name: 'junit', version: '4.11'
    testCompile group: 'org.hamcrest', name: 'hamcrest-all', version: '1.3'
    testCompile group: 'org.mockito', name: 'mockito-all', version: '1.9.5'
}

sourceSets {
    main {
        compileClasspath += configurations.ideaSdkLibs
    }
    test {
        compileClasspath += configurations.ideaSdkLibs
        runtimeClasspath += configurations.ideaSdkLibs
    }
}

task zip(type: Zip, dependsOn: test) {
    into('CheckStyle-IDEA/lib') {
        from configurations.runtime
        from configurations.runtime.allArtifacts.files
    }
}

task installPlugin(dependsOn: zip) << {
    delete "${System.properties['user.home']}/$pluginDir/CheckStyle-IDEA"
    copy {
        from zipTree("${project.buildDir}/${project.distsDirName}/${project.name}-${project.version}.zip")
        into file("${System.properties['user.home']}/$pluginDir")
    }
}
