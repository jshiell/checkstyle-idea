import org.infernus.idea.checkstyle.build.CheckstyleVersions
import org.infernus.idea.checkstyle.build.CopyCheckstyleArtifactsToSandboxTask
import org.infernus.idea.checkstyle.build.CopyClassesToSandboxTask
import org.infernus.idea.checkstyle.build.CsaccessTestTask
import org.infernus.idea.checkstyle.build.CustomSourceSetCreator
import org.infernus.idea.checkstyle.build.GatherCheckstyleArtifactsTask

buildscript {
    repositories {
        maven { url 'http://dl.bintray.com/jetbrains/intellij-plugin-service' }
    }
}

plugins {
    id 'java'
    id 'jacoco'
    id 'org.jetbrains.intellij' version '0.1.10'
}

apply plugin: 'org.jetbrains.intellij'

repositories {
    mavenLocal()
    jcenter()
}

sourceCompatibility = 1.8
version = '5.0.0'

intellij {
    version = 'IC-15.0.6';
    pluginName 'CheckStyle-IDEA'
    updateSinceUntilBuild = false
}

new CustomSourceSetCreator(project)
        .establishCsAccessSourceSet()
        .establishCsAccessTestSourceSet();
// TODO csaccessTest should be recognized as test directories in IntelliJ

// - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
//   Dependencies
// - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -

ext {
    // The Checkstyle version given here is the *base version* against which our code is initially compiled.
    // This is mostly for convenience, so we don't have to use reflection overly much. At runtime, the Checkstyle
    // version will depend on the plugin configuration.
    checkstyleBaseVersion = '7.1.1';
}

dependencies {
    compile group: 'commons-io', name: 'commons-io', version: '2.4';

    csaccessCompileOnly (group: 'com.puppycrawl.tools', name: 'checkstyle', version: checkstyleBaseVersion) {
        exclude group: 'commons-logging', module: 'commons-logging';
    }

    testCompile group: 'junit', name: 'junit', version: '4.12';
    testCompile group: 'org.hamcrest', name: 'hamcrest-all', version: '1.3';
    testCompile group: 'org.mockito', name: 'mockito-core', version: '1.10.19';
    testCompile files("${System.properties['java.home']}/../lib/tools.jar")
}


// - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
//   Tests
// - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -

ext {
    // testConfigClosure is used by all tasks of type Test (runCsaccessTests, test, xtest_*):
    testConfigClosure = {
        //beforeTest { final TestDescriptor descriptor ->
        //    logger.lifecycle("Running test: " + descriptor)
        //}
        testLogging {
            showStackTraces = true;
            showExceptions = true;
            showCauses = true;
            exceptionFormat 'full'
        }
        afterSuite { final TestDescriptor descriptor, final TestResult testResult ->
            if (descriptor.getParent() == null) {
                logger.lifecycle('\r\t' + testResult.testCount + ' tests executed, ' +
                        testResult.successfulTestCount + ' successful, ' +
                        testResult.skippedTestCount + ' skipped.');
            }
        }
    }
}
test((Closure) testConfigClosure);
Task rct = tasks.create(CsaccessTestTask.NAME, CsaccessTestTask.class);
rct.setCheckstyleVersion((String) checkstyleBaseVersion, true);
tasks.test.dependsOn rct;


// - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
//   Cross-check 'csaccessTest' unit tests against the different Checkstyle runtimes
// - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -

task xtest {
    setGroup(LifecycleBasePlugin.VERIFICATION_GROUP);
    setDescription('Runs the \'' + CustomSourceSetCreator.CSACCESSTEST_SOURCESET_NAME +
            '\' unit tests against all supported Checkstyle runtimes.');
}
tasks.check.dependsOn tasks.xtest;
new CheckstyleVersions(project).versions.each { final String csVersion ->
    if (checkstyleBaseVersion != csVersion) {
        CsaccessTestTask xt = tasks.create(CsaccessTestTask.getTaskName(csVersion), CsaccessTestTask.class);
        xt.setCheckstyleVersion(csVersion, false);
        tasks.xtest.dependsOn(xt);
    }
}


// - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
//   Exclusive test coverage report for 'csaccess' classes
// - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -

test.jacoco.enabled = false;
task jacocoTestReport4csaccess(type: JacocoReport, dependsOn: tasks.withType(CsaccessTestTask)) {
    description 'Generate exclusive JaCoCo test report on the \'' + CustomSourceSetCreator.CSACCESS_SOURCESET_NAME +
            '\' classes';
    reports {
        xml.enabled = true;
        csv.enabled = false;
        html.enabled = true;
    }
    def SourceSet csaccessSourceSet = sourceSets.getByName(CustomSourceSetCreator.CSACCESS_SOURCESET_NAME);
    classDirectories = files(csaccessSourceSet.output.classesDir);
    sourceDirectories = csaccessSourceSet.java.sourceDirectories;
    executionData = files(tasks.withType(CsaccessTestTask)*.name.collect {
        new File("${buildDir}/jacoco", "${it}.exec")
    });
    // TODO We should fail the build if a certain coverage is not reached. This will be possible with Gradle 4.0 (see
    // https://github.com/gradle/gradle/issues/824) or with a handcrafted workaround such as
    // https://github.com/springfox/springfox/blob/fb780ee1f14627b239fba95730a69900b9b2313a/gradle/coverage.gradle
}
tasks.remove(tasks.jacocoTestReport);
tasks.xtest.dependsOn tasks.jacocoTestReport4csaccess;


// - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
//   Extra tasks for packaging Checkstyle in different versions into the plugin distribution
// - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -

tasks.create(GatherCheckstyleArtifactsTask.NAME, GatherCheckstyleArtifactsTask.class);
task copyCheckstyleArtifactsToSandbox(type: CopyCheckstyleArtifactsToSandboxTask);
task copyCheckstyleArtifactsToTestSandbox(type: CopyCheckstyleArtifactsToSandboxTask) { setTest(); }
task copyClassesToSandbox(type: CopyClassesToSandboxTask);
task copyClassesToTestSandbox(type: CopyClassesToSandboxTask) { setTest(); }

// Defer some of the wiring until after the intellij plugin's tasks have been created:
tasks.all { final Task task ->
    if ('buildPlugin' == task.name || 'runIdea' == task.name) {
        task.dependsOn(tasks.copyCheckstyleArtifactsToSandbox);
        task.dependsOn tasks['copyClassesToSandbox'];
    }
    else if ('prepareSandbox' == task.name) {
        tasks['copyCheckstyleArtifactsToSandbox'].dependsOn task;
        tasks['copyClassesToSandbox'].dependsOn task;
    }
    else if ('prepareTestsSandbox' == task.name) {
        tasks['copyCheckstyleArtifactsToTestSandbox'].dependsOn task;
        tasks['copyClassesToTestSandbox'].dependsOn task;
    }
}
