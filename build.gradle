buildscript {
    repositories {
        maven { url 'https://dl.bintray.com/jetbrains/intellij-plugin-service' }
    }
}
plugins {
    id 'java'
    id 'jacoco'
    id 'idea'
    id 'org.jetbrains.intellij' version '1.0'
    id 'com.dorongold.task-tree' version '1.5'
    id 'org.infernus.idea.checkstyle.build'
}

repositories {
    mavenCentral()
}

// - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
//   Project Metadata
// - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -

version = '5.54.0'

intellij {
    version = 'IC-2020.1.4'
    pluginName = 'CheckStyle-IDEA'
    updateSinceUntilBuild = false
    instrumentCode = false
    plugins = ['java']
}

publishPlugin {
    token = System.getenv("JETBRAINS_PLUGIN_REPO_TOKEN")
}


// - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
//   Compilation
// - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -

sourceCompatibility = JavaVersion.VERSION_11
targetCompatibility = JavaVersion.VERSION_11

tasks.withType(JavaCompile) {
    options.encoding = 'UTF-8'
    options.compilerArgs << '-Xlint:deprecation'
    if (it.name == 'compileCsaccessJava' || it.name == 'compileCsaccessTestJava') {
        options.compilerArgs << '-Xlint:unchecked'
    }
}


// - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
//   Dependencies
// - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -

dependencies {
    implementation group: 'commons-io', name: 'commons-io', version: '2.8.0'
    implementation group: 'commons-codec', name: 'commons-codec', version: '1.15'
    implementation group: 'org.slf4j', name: 'jcl-over-slf4j', version: '1.7.25'
    implementation group: 'org.slf4j', name: 'slf4j-log4j12', version: '1.7.25'

    csaccessCompileOnly ("com.puppycrawl.tools:checkstyle:${supportedCsVersions.getBaseVersion()}") {
        exclude group: 'commons-logging', module: 'commons-logging'
    }

    testImplementation group: 'junit', name: 'junit', version: '4.12'
    testImplementation group: 'org.hamcrest', name: 'hamcrest', version: '2.1'
    testImplementation group: 'org.mockito', name: 'mockito-core', version: '2.23.4'
    testImplementation files("${System.properties['java.home']}/../lib/tools.jar")
}


// - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
//   IntelliJ IDEA Config
// - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -

idea.module {
    downloadJavadoc = true
    downloadSources = true
    excludeDirs += file('.idea')
    excludeDirs += file('_support')

    // TODO We should also tell IntelliJ automatically that csaccessTest contains test code.
    // The following lines should really do it, but currently don't, which seems like a Gradle bug to me:
    //def SourceSet catSourceSet = sourceSets.getByName(CustomSourceSetCreator.CSACCESSTEST_SOURCESET_NAME);
    //testSourceDirs += catSourceSet.getJava().getSrcDirs();
    //testSourceDirs += catSourceSet.getResources().getSrcDirs();
    //scopes.TEST.plus += [configurations.getByName(catSourceSet.getRuntimeConfigurationName())];
}
